package UAV
public
	
	with MC;
	with FC;
	with UAS;
	with CASE_Prop;
	
	-- UAV on-board serial bus
	bus Serial
		properties
			CASE_Prop::COMM_MODALITY => SERIAL;
	end Serial;
	
	bus implementation Serial.Impl
	end Serial.Impl;
	
	
	-- UAV on-board wifi bus
	bus Wifi
		properties
			CASE_Prop::COMM_MODALITY => WIFI;
	end Wifi;
	
	bus implementation Wifi.Impl
	end Wifi.Impl;
	
	
	-- UAV on-board camera
	device Camera
		features
			gimbal_command: in event data port;
			WIFIA: requires bus access Wifi.Impl;
	end Camera;
	
	device implementation Camera.Impl
	end Camera.Impl;
	
	
	-- UAV
	system UAV
		features
			recv_map: in event data port;
			send_status: out event data port;
			RFA: requires bus access UAS::RF.Impl;
		properties
			CASE_Prop::PHYSICAL_BOUNDARY => True;
	end UAV;
	
	system implementation UAV.Impl
		subcomponents
			MCMP: system MC::MissionComputer.Impl;
			FCTL: system FC::FlightController.Impl;
			SBUS: bus Serial.Impl;
			CAM: device Camera.Impl;
			WBUS: bus Wifi.Impl;
		connections
			bac1: bus access SBUS <-> MCMP.UARTA;
			bac2: bus access SBUS <-> FCTL.UARTA;
			bac3: bus access RFA -> MCMP.RFA;
			bac4: bus access WBUS <-> MCMP.WIFIA;
			bac5: bus access WBUS <-> CAM.WIFIA;
			c1: port recv_map -> MCMP.recv_map;
			c2: port MCMP.send_status -> send_status;
			c3: port MCMP.waypoint -> FCTL.waypoint;
			c4: port FCTL.position_status -> MCMP.position_status;
			c5: port MCMP.gimbal_command -> CAM.gimbal_command;
		properties
			Actual_Connection_Binding => (reference (SBUS)) applies to c3, c4;
			
	end UAV.Impl;
	
end UAV;